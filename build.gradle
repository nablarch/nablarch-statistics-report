group = 'com.nablarch.tool'
version = '1.1.0'
description = 'ログ集計機能'

repositories {
  mavenLocal()
  maven { url "${developLibUrl}" }
  jcenter()

  // サードパーティ製ライブラリの格納リポジトリ
  if(project.hasProperty('nablarchExtRepoUrl')) {
     maven { url "${nablarchExtRepoUrl}" }
  }
}

buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:2.2.3"
    classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
  }
}
apply {
  plugin 'com.nablarch.dev.nablarch-build'
  plugin 'com.nablarch.dev.nablarch-sonarqube'
  plugin 'com.nablarch.dev.nablarch-maven-deploy'
  from 'https://github.com/nablarch/nablarch-gradle-plugin/blob/develop/scripts/cobertura/cobertura-0.0.1.gradle?raw=true'
}
apply plugin: "io.spring.dependency-management"

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencyManagement {
  imports {
    mavenBom 'com.nablarch.profile:nablarch-bom:5u8'
  }
  applyMavenExclusions false
}

configurations {
  testCompile.exclude module: 'hamcrest-core'
}

dependencies {

  compile 'com.nablarch.framework:nablarch-fw-batch'
  compile 'com.nablarch.framework:nablarch-common-date'

  testRuntime 'com.nablarch.framework:nablarch-testing'
  testRuntime 'com.nablarch.framework:nablarch-common-jdbc'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  
  // JDBC Drivers
  testRuntime (group: 'com.oracle', name: 'ojdbc6', version: '11.2.0.4.0')
  testRuntime (group: 'com.oracle', name: 'ucp', version: '11.2.0.3.0')
}


test {
  ignoreFailures = true
  
  // find component definition
  classpath = files("${projectDir}/test/java") + files("${projectDir}/test/temp") + project.sourceSets.test.runtimeClasspath
}



jar {
  manifest {
    attributes(
      'Created-By' : 'Gradle ' + project.gradle.gradleVersion,
      'Build-Jdk': System.properties['java.version'] + ' (' + System.properties['java.vendor'] + ')',
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
    )
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}
